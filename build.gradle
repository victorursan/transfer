plugins {
    id 'java'
    id 'application'
    id 'java-library'
    id 'idea'
}

group 'com.victor.bank'
version '1.0.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'com.h2database', name: 'h2', version: '1.4.199'
    implementation group: 'com.zaxxer', name: 'HikariCP', version: '3.3.1'
    implementation group: 'io.undertow', name: 'undertow-core', version: '2.0.25.Final'

    implementation group: 'com.typesafe', name: 'config', version: '1.3.4'

    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.9.9'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.9.3'
    implementation group: 'com.fasterxml.jackson.module', name: 'jackson-module-afterburner', version: '2.9.9'
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: '2.9.9'


    api group: 'org.slf4j', name: 'slf4j-api', version: '1.7.28'
    implementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.28'

    api group: 'org.jboss.xnio', name: 'xnio-api', version: '3.7.3.Final'
    implementation group: 'org.jboss.xnio', name: 'xnio-nio', version: '3.7.3.Final'
    implementation group: 'org.jboss.threads', name: 'jboss-threads', version: '2.3.3.Final'

    annotationProcessor group: 'org.jetbrains', name: 'annotations', version: '17.0.0'
    compileOnly group: 'org.jetbrains', name: 'annotations', version: '17.0.0'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.8'
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.8'

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.5.1'
    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '2.1'
    testImplementation group: 'com.github.npathai', name: 'hamcrest-optional', version: '2.0.0'

}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

sourceSets {
    main {
        resources {
            srcDirs "src/main/resources"
        }
    }
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/it/java')
        }
        resources.srcDir file('src/it/resources')
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    useJUnitPlatform()
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

application {
    mainClassName = 'com.victor.bank.transfer.Main'
}

sourceCompatibility = JavaVersion.VERSION_12
targetCompatibility = JavaVersion.VERSION_12